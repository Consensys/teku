// See https://github.com/tbroyer/gradle-apt-plugin
// TODO Determine if net.ltgt.apt is even necessary.

plugins {
  id 'net.ltgt.apt' version '0.19'
}

// we use this config to get the path of the JDK 9 javac jar, to
// stick it in the bootclasspath when running tests
configurations.maybeCreate("epJavac")

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
  implementation 'com.google.errorprone:error_prone_annotation'
  implementation 'com.google.errorprone:error_prone_core'
  implementation 'com.google.auto.service:auto-service'

  annotationProcessor 'com.google.auto.service:auto-service'

  testImplementation 'com.google.errorprone:error_prone_test_helpers'

  epJavac 'com.google.errorprone:error_prone_check_api'
}

test {
  if (JavaVersion.current().isJava8()) {
    enabled = false
    logger.info("Disabling {} because errorprone tests always fail in Java {}",
      project.name, JavaVersion.current().majorVersion)
  }

  testLogging { showStandardStreams = true }
}
